apply plugin: "java"

ext.cxfVersion = '2.7.10'

ext.wsdlFile = file("src/main/resources/schemas/wsdl/HsaWsInteraction_3.18.wsdl")
ext.generatedJavaDir = file("build/generatedSource/java")

sourceSets.main.java {
    srcDir generatedJavaDir
}

configurations {
    wsdlgen
}

task wsdl2Java(type: JavaExec) {
    inputs.files wsdlFile
    outputs.dir generatedJavaDir
    classpath configurations.wsdlgen
    main "org.apache.cxf.tools.wsdlto.WSDLToJava"
    args "-xjc-XhashCode", "-xjc-Xequals", "-xjc-XtoString", "-xjc-Xinheritance",
    "-xjc-b,${projectDir}/src/main/resources/jaxb-bindings.xjb", "-validate", "-verbose",
    "-p", "urn:riv:hsa:HsaWs:3=se.inera.ifv.hsaws.v3", 
    "-p", "urn:riv:hsa:HsaWsResponder:3=se.inera.ifv.hsawsresponder.v3", 
    "-p", "http://www.w3.org/2005/08/addressing=org.w3.wsaddressing10", '-d', generatedJavaDir.path, wsdlFile
}

compileJava.dependsOn(wsdl2Java)

dependencies {
    compile "joda-time:joda-time:2.3"
    compile "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4"
        
    wsdlgen "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:${cxfVersion}"
    wsdlgen "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:${cxfVersion}"
    wsdlgen "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4"
}

repositories {
    mavenLocal()
    mavenCentral()
}
